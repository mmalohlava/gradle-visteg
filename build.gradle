plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'idea'
}

dependencies {
  implementation gradleApi()
  implementation localGroovy()
}

repositories {
  mavenCentral()
}

dependencies {
    testImplementation('junit:junit:4.11')
}

/**
 * Configure functional testing.
 */
sourceSets {
    funcTest {
        groovy {
            srcDir file('src/funcTest/groovy')
        }
        resources {
            srcDir file('src/funcTest/resources')
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

task funcTest(type: Test) {
    description = "Functional tests."
    group = "verification"
    testClassesDirs = sourceSets.funcTest.output.classesDirs
    classpath = sourceSets.funcTest.runtimeClasspath
}

check.dependsOn funcTest

dependencies {
    funcTestImplementation('org.spockframework:spock-core:1.3-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}


/*
 * Configure publication into Gradle Plugin Portal.
 */
pluginBundle {
    website = 'https://github.com/mmalohlava/gradle-visteg'
    vcsUrl = 'https://github.com/mmalohlava/gradle-visteg.git'
    tags = ['gradle', 'graphviz', 'plugin', 'visteg']
}

gradlePlugin {
    testSourceSets sourceSets.funcTest

    plugins {
        vistegPlugin {
            id = 'cz.malohlava.visteg'
            displayName = 'Gradle plugin exporting task execution graph into .dot file.'
            description = 'Gradle plugin exporting task execution graph into .dot file.'
            implementationClass = 'cz.malohlava.VisTaskExecGraphPlugin'
        }
    }
}

